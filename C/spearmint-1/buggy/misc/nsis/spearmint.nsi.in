; NSIS description file for Spearmint installer
;
; you have to copy OpenAL32.dll here manually

!define VERSION "XXXVERSIONXXX"
!define RELEASE "XXXRELEASEXXX"

!define PUBLISHER "The Spearmint Team"
!define URL "https://clover.moe/spearmint"

!define GAMENAME "Spearmint"
!define CLIENT "spearmint_x86.exe"
!define SERVER "spearmint-server_x86.exe"
!define RENDERER_PREFIX "spearmint-renderer-"
!define GAMEDIR "Spearmint"
!define GAMEKEY "spearmint"
!define PROTOCOL_HANDLER "spearmint"

!define MULTIUSER_MUI
!define MULTIUSER_EXECUTIONLEVEL Highest
!define MULTIUSER_INSTALLMODE_COMMANDLINE
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY "Software\${GAMEKEY}"
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME "Install_Mode"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY "Software\${GAMEKEY}"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME "Install_Dir"
!define MULTIUSER_INSTALLMODE_INSTDIR "${GAMEDIR}"
!include MultiUser.nsh

!include "MUI2.nsh"
!define MUI_ICON "../spearmint.ico"

; The name of the installer
Name "${GAMENAME} ${VERSION}"

; The file to write
OutFile "XXXFILENAMEXXX"

; The default installation directory
; set by Multiuser.nsh
;InstallDir "$PROGRAMFILES\${GAMEDIR}"

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
; handled by Multiuser.nsh
;InstallDirRegKey HKLM "Software\${GAMEKEY}" "Install_Dir"

;--------------------------------
;Interface Settings

!define MUI_ABORTWARNING

;--------------------------------
;Pages

!insertmacro MULTIUSER_PAGE_INSTALLMODE
;!insertmacro MUI_PAGE_LICENSE "../../COPYING.txt"
!define MUI_COMPONENTSPAGE_SMALLDESC
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English"

;--------------------------------

;--------------------------------
;Multiuser stuff
Function .onInit
  !insertmacro MULTIUSER_INIT
FunctionEnd

Function un.onInit
  !insertmacro MULTIUSER_UNINIT
FunctionEnd

; The stuff to install
Section "${GAMENAME} ${VERSION}-${RELEASE} (required)" ioquake3

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  File "../../build/release-mingw32-x86/SDL2.dll"
!ifndef USE_INTERNAL_SPEEX
  File "libspeex.dll"
!endif
!ifndef USE_INTERNAL_ZLIB
  File "zlib1.dll"
!endif
!ifndef USE_INTERNAL_JPEG
  File "jpeg8c.dll"
!endif

  ; Put file there
  File "../../build/release-mingw32-x86/${SERVER}"
  File "../../build/release-mingw32-x86/${CLIENT}"
!ifdef USE_RENDERER_DLOPEN
  File "../../build/release-mingw32-x86/${RENDERER_PREFIX}opengl1_x86.dll"
  File "../../build/release-mingw32-x86/${RENDERER_PREFIX}opengl2_x86.dll"
!endif

  File "../../COPYING.txt"
  File "/oname=README.txt" "../../README.md"
  File "../../id-readme.txt"
  File "../../voip-readme.txt"

  ; Write the installation path into the registry
  WriteRegStr SHCTX "Software\${GAMEKEY}" ${MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME} "$INSTDIR"
  WriteRegStr SHCTX "Software\${GAMEKEY}" ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME} "$MultiUser.InstallMode"

  ; Write the uninstall keys for Windows
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "DisplayName" "${GAMENAME}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "DisplayVersion" "${VERSION}-${RELEASE}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "Publisher" "${PUBLISHER}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "URLInfoAbout" "${URL}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "NoModify" 1
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts" StartMenuShortcuts

  CreateDirectory "$SMPROGRAMS\${GAMENAME}"
  CreateShortCut "$SMPROGRAMS\${GAMENAME}\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\${GAMENAME}\${GAMENAME}.lnk" "$INSTDIR\${CLIENT}" "" "$INSTDIR\${CLIENT}" 0

SectionEnd

Section "Protocol Handler" ProtocolHandler

  WriteRegStr SHCTX "Software\Classes\${PROTOCOL_HANDLER}" "CustomUrlApplication" "$INSTDIR\${CLIENT}"
  WriteRegStr SHCTX "Software\Classes\${PROTOCOL_HANDLER}" "CustomUrlArguments" '"%1"'
  WriteRegStr SHCTX "Software\Classes\${PROTOCOL_HANDLER}" "URL Protocol" ""
  WriteRegStr SHCTX "Software\Classes\${PROTOCOL_HANDLER}\DefaultIcon" "" "$INSTDIR\${CLIENT},0"
  WriteRegStr SHCTX "Software\Classes\${PROTOCOL_HANDLER}\shell\open\command" "" '"$INSTDIR\${CLIENT}" --uri "%1"'

SectionEnd

!ifdef USE_OPENAL_DLOPEN
Section "OpenAL-Soft library" OpenAL

  SetOutPath $INSTDIR

  File "OpenAL32.dll"

SectionEnd
!endif

!ifdef USE_CURL_DLOPEN
Section "libcurl" libcurl

  SetOutPath $INSTDIR

  File "libcurl-4.dll"

SectionEnd
!endif

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}"
  DeleteRegKey SHCTX "Software\${GAMEKEY}"
  DeleteRegKey SHCTX "Software\Classes\${PROTOCOL_HANDLER}"

  ; Remove files and uninstaller
  Delete $INSTDIR\${CLIENT}
  Delete $INSTDIR\${SERVER}
!ifdef USE_RENDERER_DLOPEN
  Delete $INSTDIR\${RENDERER_PREFIX}opengl1_x86.dll
  Delete $INSTDIR\${RENDERER_PREFIX}opengl2_x86.dll
!endif

  Delete $INSTDIR\COPYING.txt
  Delete $INSTDIR\README.txt
  Delete $INSTDIR\id-readme.txt
  Delete $INSTDIR\voip-readme.txt

  Delete $INSTDIR\SDL2.dll
!ifndef USE_INTERNAL_SPEEX
  Delete $INSTDIR\libspeex.dll
!endif
!ifndef USE_INTERNAL_ZLIB
  Delete $INSTDIR\zlib1.dll
!endif
!ifndef USE_INTERNAL_JPEG
  Delete $INSTDIR\jpeg8c.dll
!endif

!ifdef USE_OPENAL_DLOPEN
  Delete $INSTDIR\OpenAL32.dll
!endif
!ifdef USE_CURL_DLOPEN
  Delete $INSTDIR\libcurl-4.dll
!endif

  Delete $INSTDIR\uninstall.exe

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\${GAMENAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${GAMENAME}\${GAMENAME}.lnk"

  ; Remove directories used
  RMDir "$SMPROGRAMS\${GAMENAME}"
  RMDir "$INSTDIR"

SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${ioquake3} "The game executables."
  !insertmacro MUI_DESCRIPTION_TEXT ${StartMenuShortcuts} "Create shortcuts in the start menu."
  !insertmacro MUI_DESCRIPTION_TEXT ${ProtocolHandler} "The protocol handler lets you connect to a game by clicking a link in a web browser."
!ifdef USE_OPENAL_DLOPEN
  !insertmacro MUI_DESCRIPTION_TEXT ${OpenAL} "OpenAL files."
!endif
!ifdef USE_CURL_DLOPEN
  !insertmacro MUI_DESCRIPTION_TEXT ${libcurl} "libcurl files."
!endif
!insertmacro MUI_FUNCTION_DESCRIPTION_END
